pkgname=mingw-w64-binutils
pkgver=2.40
pkgrel=1
arch=(x86_64)
depends=(zlib)
makedepends=(gcc)
cross_targets="i686-w64-mingw32 x86_64-w64-mingw32"
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.gz)

prepare() {
	cd binutils-$pkgver

	sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

	mkdir -pv $srcdir/{i686-w64-mingw32,x86_64-w64-mingw32}
}

build() {
	for ctarget in $cross_targets; do
		echo "Compiling $ctarget binutils"

		cd $srcdir/$ctarget

		./../binutils-$pkgver/configure \
		--prefix=/usr \
		--target=$ctarget \
		--infodir=/usr/share/info/$ctarget \
		--enable-lto \
		--enable-plugins \
		--enable-deterministic-archives \
		--enable-multilib \
		--disable-nls \
		--disable-werror

		make

		cd ..
	done
}

package() {
	make DESTDIR=$pkgdir -C $srcdir/i686-w64-mingw32 install
	make DESTDIR=$pkgdir -C $srcdir/x86_64-w64-mingw32 install

	rm -v "$pkgdir"/usr/lib/bfd-plugins/libdep.so
}
