pkgname=glslang
pkgver=12.2.0
stools=2023.2
sheaders=1.3.250.0
pkgrel=1
pkgdesc='OpenGL and OpenGL ES shader front end and validator'
arch=('x86_64')
depends=('gcc' 'llvm' 'clang')
makedepends=('cmake' 'ninja' 'git' 'python3')
# Get the commits from known_good.json for every release
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/glslang/archive/${pkgver}.tar.gz
	SPIRV-Tools.tar.gz::https://github.com/KhronosGroup/SPIRV-Tools/archive/refs/tags/v$stools.tar.gz
	SPIRV-Headers.tar.gz::https://github.com/KhronosGroup/SPIRV-Headers/archive/refs/tags/sdk-$sheaders.tar.gz)

#
# Spirv headers + tools are included here
# WHY? = Because glslang itself already provides some spirv related files that have conflicted before, to skip the issue we build spirv-* here and package everything

prepare() {
	mv $srcdir/SPIRV-Tools-$stools $srcdir/$pkgname-$pkgver/External/spirv-tools
	mv $srcdir/SPIRV-Headers-sdk-$sheaders $srcdir/$pkgname-$pkgver/External/spirv-tools/external/spirv-headers
}

build() {
	## GLSLANG
	cd ${pkgname}-${pkgver}
	export CXXFLAGS+=" -ffat-lto-objects"

	# SHARED
	cmake \
	-B build-shared \
	-G Ninja \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D CMAKE_BUILD_TYPE=Release \
	-D BUILD_SHARED_LIBS=ON

	ninja -C build-shared

	# STATIC
	cmake \
	-B build-static \
	-G Ninja \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D CMAKE_BUILD_TYPE=Release \
	-D BUILD_SHARED_LIBS=OFF

	ninja -C build-static

	## SPIRV-Headers

	## SPIRV-Tools
}

package() {
  cd ${pkgname}-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build-shared install
  DESTDIR="${pkgdir}" ninja -C build-static install

  cd "${pkgdir}"/usr/lib
  for lib in *.so; do
    ln -sf "${lib}" "${lib}.0"
  done
}
